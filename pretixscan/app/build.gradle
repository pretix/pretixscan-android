plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "org.jetbrains.kotlin.kapt"
    id "app.cash.sqldelight"
    id "com.jaredsburrows.license"
}

def flavors = [:]
try {
    apply from: 'flavors.gradle'
    flavors.putAll(addflavors)
} catch(UncheckedIOException ignored) {
    // Optional
}

apply from: '../libpretixsync-repo/libpretixsync/versions.gradle'

android {
    namespace 'eu.pretix.pretixscan.droid'

    compileSdk 36

    defaultConfig {
        applicationId "eu.pretix.pretixscan.droid"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 102
        versionName "3.0.0-beta3"
        vectorDrawables.useSupportLibrary = true
        vectorDrawables.generatedDensities = ['ldpi', 'mdpi', 'hdpi', 'xhdpi', 'xxhdpi']
        multiDexEnabled true

        setProperty("archivesBaseName", rootProject.name + "-" + versionName)
    }

    buildFeatures {
        buildConfig = true
        dataBinding = true
        viewBinding = true
    }

    kotlin {
        jvmToolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
        flavors.each { flavorName, flavorData ->
            "$flavorName" {
                storeFile file("../../${flavorName}.keystore")
                storePassword System.getenv("KSTOREPWD")
                keyAlias "pretixdroid"
                keyPassword System.getenv("KSTOREPWD")
            }
        }
    }

    buildTypes {
		def sentry_dsn = ""
		try {
			Properties properties = new Properties()
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = ""
            }
		} catch (ignore) {
		}

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            multiDexKeepProguard file('multidex-config.pro')
            manifestPlaceholders["SENTRY_DSN"] = sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            manifestPlaceholders["SENTRY_DSN"] = ""
        }
    }

    flavorDimensions "brand"
    productFlavors {
        pretix {
            dimension "brand"
            signingConfig signingConfigs.release
        }
        flavors.each { flavorName, flavorData ->
            "$flavorName" {
                dimension "${flavorData['dimension']}"
                applicationId "${flavorData['appId']}"
                signingConfig signingConfigs."$flavorName"
            }
        }
    }
    packagingOptions {
        resources {
            excludes += [
                'META-INF/DEPENDENCIES',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/license.txt',
                'META-INF/NOTICE',
                'META-INF/NOTICE.txt',
                'META-INF/notice.txt',
                'META-INF/ASL2.0'
            ]
        }
    }

    lint {
        disable 'InvalidPackage', 'MissingTranslation'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation 'androidx.sqlite:sqlite:2.2.0'  // 2.4.0 requires compileSDK 34

    implementation 'com.louiscad.splitties:splitties-toast:3.0.0'
    implementation 'com.github.traex.rippleeffect:library:1.3'
    implementation 'com.github.kizitonwose:CalendarView:1.0.4'

    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:okhttp-tls:$okhttp_version"
    implementation 'io.sentry:sentry-android:8.18.0'
    implementation 'org.slf4j:slf4j-nop:1.7.30'
    implementation "joda-time:joda-time:$joda_version"
    implementation 'net.zetetic:sqlcipher-android:4.5.6'
    implementation "net.sourceforge.streamsupport:streamsupport-cfuture:$cfuture_version"
    implementation "com.fasterxml.jackson.core:jackson-databind:$jackson_core_version"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_kotlin_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jackson_kotlin_version"
    implementation "net.i2p.crypto:eddsa:$eddsa_version"
    implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
    implementation "app.cash.sqldelight:android-driver:$sqldelight_version"

    // libpretixui
    implementation(project(':libpretixui-android')) {
        transitive = true
    }
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    // libpretixsync
    implementation(project(':libpretixsync')) {
        transitive = false
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.apg-mobile:android-round-textview:v1.0.0'
}

sqldelight {
    databases {
        SyncDatabase {
            packageName = "eu.pretix.pretixscan.sqldelight"

            dependency(project(":libpretixsync"))

            // Oldest dialect supported by SQLDelight 2.1.0
            // In Android projects, it will auto-select based on SDK
            // but not go lower than 3.18 (Android 9 is still on 3.8)
            dialect "app.cash.sqldelight:sqlite-3-18-dialect:$sqldelight_version"
        }
    }
}
