apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.sentry.android.gradle'

def flavors = [:]
try {
    apply from: 'flavors.gradle'
    flavors.putAll(addflavors)
} catch(UncheckedIOException ignored) {
    // Optional
}

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "eu.pretix.pretixscan.droid"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 29
        versionName "1.6.1"
        vectorDrawables.useSupportLibrary = true
        vectorDrawables.generatedDensities = ['ldpi', 'mdpi', 'hdpi', 'xhdpi', 'xxhdpi']
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
        flavors.each { flavorName, flavorData ->
            "$flavorName" {
                storeFile file("../../${flavorName}.keystore")
                storePassword System.getenv("KSTOREPWD")
                keyAlias "pretixdroid"
                keyPassword System.getenv("KSTOREPWD")
            }
        }
    }

    buildTypes {
		def sentry_dsn = "null"
		try {
			Properties properties = new Properties()
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = "null"
            } else {
                sentry_dsn = "\"" + sentry_dsn + "\""
            }
		} catch (ignore) {
		}

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
        }
    }

    flavorDimensions "brand"
    productFlavors {
        pretix {
            dimension "brand"
            signingConfig signingConfigs.release
        }
        flavors.each { flavorName, flavorData ->
            "$flavorName" {
                dimension "${flavorData['dimension']}"
                applicationId "${flavorData['appId']}"
                signingConfig signingConfigs."$flavorName"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

sentry {
    autoProguardConfig true
    autoUpload true
}

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains.anko:anko:0.10.6'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta4'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.paging:paging-runtime:2.1.1'

    implementation 'com.github.kizitonwose.time:time-android:1.0.1'
    implementation 'com.github.traex.rippleeffect:library:1.3'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'

    implementation 'com.squareup.okhttp3:okhttp:4.3.0'
    implementation 'io.sentry:sentry-android:1.7.29'
    implementation 'org.slf4j:slf4j-nop:1.7.30'
    implementation 'joda-time:joda-time:2.9.9'
    implementation 'io.requery:requery:1.6.0'
    implementation 'io.requery:requery-android:1.6.0'
    implementation 'io.requery:requery-kotlin:1.6.0'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.9'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.1'
    implementation "com.fasterxml.jackson.core:jackson-databind:2.10.1"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.10.1"
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.0'

    implementation 'com.facebook.soloader:soloader:0.9.0'
    debugImplementation 'com.facebook.flipper:flipper:0.52.1'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.52.1'

    releaseImplementation 'com.facebook.flipper:flipper-noop:0.52.1'

    annotationProcessor 'io.requery:requery-processor:1.6.0'
    implementation(project(':libpretixsync')) {
        transitive = false
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.apg-mobile:android-round-textview:v1.0.0'
}
kapt {
    generateStubs = true
}
