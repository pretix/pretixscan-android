apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def flavors = [:]
try {
    apply from: 'flavors.gradle'
    flavors.putAll(addflavors)
} catch(UncheckedIOException ignored) {
    // Optional
}

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId "eu.pretix.pretixscan.droid"
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 65
        versionName "1.13.4"
        vectorDrawables.useSupportLibrary = true
        vectorDrawables.generatedDensities = ['ldpi', 'mdpi', 'hdpi', 'xhdpi', 'xxhdpi']
        multiDexEnabled true
    }

    dataBinding {
        enabled = true
    }
	dexOptions {
		javaMaxHeapSize "3g"
	}

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        viewBinding = true
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    lintOptions {
        disable 'InvalidPackage' // problem with jetty and bouncycastle
        disable 'MissingTranslation'
    }

    signingConfigs {
        release {
            storeFile file("../../release.keystore")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "pretixdroid"
            keyPassword System.getenv("KSTOREPWD")
        }
        flavors.each { flavorName, flavorData ->
            "$flavorName" {
                storeFile file("../../${flavorName}.keystore")
                storePassword System.getenv("KSTOREPWD")
                keyAlias "pretixdroid"
                keyPassword System.getenv("KSTOREPWD")
            }
        }
    }

    buildTypes {
		def sentry_dsn = "null"
		try {
			Properties properties = new Properties()
			properties.load(project.rootProject.file('local.properties').newDataInputStream())
            sentry_dsn = properties.getProperty('sentry.dsn')
            if (sentry_dsn == null) {
                sentry_dsn = "null"
            } else {
                sentry_dsn = "\"" + sentry_dsn + "\""
            }
		} catch (ignore) {
		}

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SENTRY_DSN", sentry_dsn
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField "String", "SENTRY_DSN", "null"
        }
    }

    flavorDimensions "brand"
    productFlavors {
        pretix {
            dimension "brand"
            signingConfig signingConfigs.release
        }
        flavors.each { flavorName, flavorData ->
            "$flavorName" {
                dimension "${flavorData['dimension']}"
                applicationId "${flavorData['appId']}"
                signingConfig signingConfigs."$flavorName"
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'org.jetbrains.anko:anko:0.10.6'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'com.github.traex.rippleeffect:library:1.3'
    implementation 'me.dm7.barcodescanner:zxing:1.9.8'
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'
    implementation 'com.github.kizitonwose:CalendarView:1.0.4'

    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'io.sentry:sentry-android:4.3.0'
    implementation 'org.slf4j:slf4j-nop:1.7.30'
    implementation 'joda-time:joda-time:2.10.10'
    implementation 'io.requery:requery:1.6.0'
    implementation 'io.requery:requery-android:1.6.0'
    implementation 'io.requery:requery-kotlin:1.6.0'
    implementation 'net.zetetic:android-database-sqlcipher:3.5.9'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.3'
    implementation "com.fasterxml.jackson.core:jackson-databind:2.13.0"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.13.0"
    implementation 'net.i2p.crypto:eddsa:0.3.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    implementation 'com.facebook.soloader:soloader:0.10.3'
    debugImplementation 'com.facebook.flipper:flipper:0.139.0'
    debugImplementation 'com.facebook.soloader:soloader:0.10.3'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.139.0'

    releaseImplementation 'com.facebook.flipper:flipper-noop:0.139.0'

    // libpretixui
    implementation(project(':libpretixui-android')) {
        transitive = true
    }
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // libpretixsync
    kapt 'io.requery:requery-processor:1.6.0'
    implementation(project(':libpretixsync')) {
        transitive = false
    }
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.apg-mobile:android-round-textview:v1.0.0'
}
kapt {
    generateStubs = true
}
